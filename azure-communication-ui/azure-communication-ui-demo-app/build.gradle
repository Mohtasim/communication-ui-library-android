plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.azure.android.communication.ui.callingCompositeDemoApp"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode ui_library_version_code
        versionName "$ui_library_version_name"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'TOKEN_FUNCTION_URL', properties.getProperty('TOKEN_FUNCTION_URL', '""')
        buildConfigField 'String', 'USER_NAME', properties.getProperty('USER_NAME', '"Mohtasim"')
        buildConfigField 'String', 'GROUP_CALL_ID', properties.getProperty('GROUP_CALL_ID', '"74fce2c0-520f-11ec-97de-71411a9a8e13"')
        buildConfigField 'String', 'TEAMS_MEETING_LINK', properties.getProperty('TEAMS_MEETING_LINK', '""')
        buildConfigField 'String', 'ACS_TOKEN', properties.getProperty('ACS_TOKEN', '"eyJhbGciOiJSUzI1NiIsImtpZCI6IjEwNCIsIng1dCI6IlJDM0NPdTV6UENIWlVKaVBlclM0SUl4Szh3ZyIsInR5cCI6IkpXVCJ9.eyJza3lwZWlkIjoiYWNzOmI2YWFkYTFmLTBiMWQtNDdhYy04NjZmLTkxYWFlMDBhMWQwMV8wMDAwMDAxMS0wYzlkLTM1NDgtMGQ4Yi0wODQ4MjIwMGJhNTQiLCJzY3AiOjE3OTIsImNzaSI6IjE2NTEwOTE2NzMiLCJleHAiOjE2NTExNzgwNzMsImFjc1Njb3BlIjoidm9pcCIsInJlc291cmNlSWQiOiJiNmFhZGExZi0wYjFkLTQ3YWMtODY2Zi05MWFhZTAwYTFkMDEiLCJpYXQiOjE2NTEwOTE2NzN9.pPQKouZOVt4hNJ_i6EbQdFmXyD8kHdykjqYfhuEz0c5celjGQz83xl_6AzPNoJL9DYRvlcIZKIGhrc_s8HhqxBqaU5KPrv-0r1zs_2EnBNtV1sWNElrbObxnpimt-lqIDuLCMCZqfOKc_LfUbniKBhsoFVF06RDSX8CBPNLfrG5vEs9oty4XKP-FIBdHiAHqtUTGnHMpAQYymfK-syZx_fVC91y_DApk2YMSezEbW2T-CUh4cQ3sU5WeiWbqIyFxat9joVDZBW6rDif7pQt9MLazbjB4__FLdoS3uLQx0oPAshKWAexrJ5ozyAt9yOdtSv0lltwSCDvaqRFjnj3fXg"')
        buildConfigField 'String', 'APP_SECRET', properties.getProperty('APP_SECRET', '"8cb48e80-76ae-43f4-939a-ab6edd255af4"')

    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE_FILEPATH")))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'acs-ui-library.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        pickFirst 'META-INF/AL2.0'
        pickFirst 'META-INF/LGPL2.1'
        pickFirst 'META-INF/azure-communication-ui_debug.kotlin_module'
        doNotStrip '**/*.so'
    }

    testOptions {
        animationsDisabled = true
        packagingOptions {
            jniLibs {
                useLegacyPackaging = true
            }
        }
        unitTests {
            returnDefaultValues = true
            all {
                failFast = true
            }
        }
    }

    flavorDimensions "distribute"
    productFlavors {
        appCenter {
            dimension "distribute"
        }
    }
}

repositories {
    flatDir{
        dirs "lib"
    }
}

dependencies {
    def appCenterSdkVersion = '4.3.1'
    implementation project(path: ':azure-communication-ui')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.activity:activity-ktx:$activity_ktx_version"
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation(name: "magnifier-0.0.17", ext: 'aar')
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    appCenterImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
    testImplementation "junit:junit:$junit"
    androidTestImplementation('com.microsoft.appcenter:espresso-test-extension:1.4')
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'net.java.dev.jna:jna:5.10.0'
    androidTestImplementation('org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0-native-mt') {
        exclude group: "net.java.dev.jna", module: "jna"
    }
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit"
    androidTestImplementation 'androidx.test:rules:1.4.1-alpha03'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_contrib"
}
